plugins {
    id "architectury-plugin" version "3.4.127"
    id "dev.architectury.loom" version "0.10.0.205" apply false
}

architectury {
    minecraft = rootProject.minecraft_version
}

def version_replaces = [
        "mod_id": rootProject.mod_id,
        "mod_version": rootProject.mod_version,
        "mod_name": rootProject.mod_name,
        "mod_description": rootProject.mod_description,
        "fabric_loader_version": rootProject.fabric_loader_version,
        "minecraft_version": rootProject.minecraft_version,
        "forge_version": rootProject.forge_version,
        "base_mc_version": rootProject.base_mc_version,
        "github": rootProject.github
]

subprojects {
    apply plugin: "dev.architectury.loom"

    repositories {
        maven { url "https://jitpack.io" }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings "net.fabricmc:yarn:${rootProject.yarn_mappings}:v2"

        modImplementation("me.shedaniel.cloth:cloth-config-fabric:${rootProject.cloth_config_version}")

        compileOnly "org.projectlombok:lombok:${rootProject.lombok_version}"
        annotationProcessor "org.projectlombok:lombok:${rootProject.lombok_version}"
    }

    processResources {
        filesMatching("fabric.mod.json") {
            expand version_replaces
        }
        filesMatching("META-INF/mods.toml") {
            expand version_replaces
        }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release.set(17)
    }

    java {
        withSourcesJar()
    }
}