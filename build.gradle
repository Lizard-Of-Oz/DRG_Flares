plugins {
    id "architectury-plugin" version "3.1.105"
    id "forgified-fabric-loom" version "0.6.96" apply false
}

architectury {
    minecraft = rootProject.minecraft_version
}

def version_replaces = [
        "mod_id": rootProject.mod_id,
        "mod_version": rootProject.mod_version,
        "mod_name": rootProject.mod_name,
        "mod_description": rootProject.mod_description,
        "fabric_loader_version": rootProject.fabric_loader_version,
        "minecraft_version": rootProject.minecraft_version,
        "forge_version": rootProject.forge_version,
        "base_mc_version": rootProject.base_mc_version,
        "github": rootProject.github
]

subprojects {
    apply plugin: "forgified-fabric-loom"

    repositories {
        maven { url "https://jitpack.io" }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings "net.fabricmc:yarn:${rootProject.yarn_mappings}:v2"

        compileOnly "org.projectlombok:lombok:${rootProject.lombok_version}"
        annotationProcessor "org.projectlombok:lombok:${rootProject.lombok_version}"
    }

    processResources {
        filesMatching("fabric.mod.json") {
            expand version_replaces
        }
        filesMatching("META-INF/mods.toml") {
            expand version_replaces
        }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"

        def targetVersion = 8
        if (JavaVersion.current().isJava9Compatible()) {
            options.release.set(targetVersion)
        }
    }

    java {
        withSourcesJar()
    }
}